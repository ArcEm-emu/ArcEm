REM by A. Macfarlane Smith
REM (C) Archifishal Software 2001
ON ERROR ERROR EXT ERR,REPORT$+" at line "+STR$ ERL

DIM code% 4000

r12_key_pointer=0
r12_mouse_pointer=4

FOR i=0 TO 4000-1 STEP 4:i!code%=0:NEXT
FOR I%=4 TO 6 STEP 2:P%=0:O%=code%
[OPT I%
EQUD    0
EQUD    initialisation
EQUD    closedown
EQUD    0
EQUD    title_string
EQUD    help_string
EQUD    0
EQUD    &53340 ; NB NOT defined
EQUD    swi_entry
EQUD    swi_list
EQUD    0
EQUD    0
EQUD    flags

.flags EQUD 1

.initialisation
STMFD   R13!,{R7-R12,R14}
LDR     R7,[R12]
CMP     R7,#0
LDMNEFD R13!,{R7-R12,PC}
:
; Claim OS_Module space
MOV     R0,#6
MOV     R3,#1024
SWI     "XOS_Module"

ADRVS   R0,fail_allocate
LDMVSFD R13!,{R7-R12,PC}

STR     R2,[R12]
:
MOV     R0,#8
STR     R0,[R2,#r12_key_pointer]

MOV     R0,#260
STR     R0,[R2,#r12_mouse_pointer]

MOV     R0,#&10 ; EventV
ADR     R1,ArcEm_KeyHandler
MOV     R2,R12
SWI     "XOS_Claim"
:
MOV     R0,#14
MOV     R1,#11 ; key transition
SWI     "XOS_Byte"
:
LDMFD   R13!,{R7-R12,PC}

.fail_allocate
EQUD    &53340
EQUS    "Failed to allocate workspace"
EQUB    0
ALIGN

.closedown
STMFD   R13!,{R7-R12,R14}
LDR     R7,[R12]
:
MOV     R0,#13
MOV     R1,#11 ; key transition
SWI     "XOS_Byte"
:
MOV     R0,#&10 ; EventV
ADR     R1,ArcEm_KeyHandler
MOV     R2,R12
SWI     "XOS_Release"
:
; Release OS_Module space
MOV     R0,#7
MOV     R2,R7
SWI     "XOS_Module"
:
LDMFD   R13!,{R7-R12,PC}

.ArcEm_KeyHandler
STMFD   R13!,{R9,R14}
MRS     R9,CPSR
CMP     R0,#11 ; Key transition
BNE     boring ; Not 11, so go away

STMFD   R13!,{R1,R2,R4,R12}
LDR     R12,[R12]
:
LDR     R4,[R12,#r12_key_pointer]
CMP     R4,#252
BGE     boring2 ; our buffer is full ;(

STR     R1,[R12,R4] ; transition value
ADD     R4,R4,#4
STR     R2,[R12,R4] ; key number
ADD     R4,R4,#4

STR     R4,[R12,#r12_key_pointer]

.boring2
LDMFD   R13!,{R1,R2,R4,R12}
.boring
MSR     CPSR_cxsf,R9
LDMFD   R13!,{R9,PC}

.title_string
.swi_list
EQUS    "ArcEmKey"+CHR$0
EQUS    "GetKey"+CHR$0
EQUB    0
ALIGN

.help_string
EQUS    "ArcEmKey"+CHR$9+CHR$9+"1.01 (22 Sep 2001)"+CHR$ 0
ALIGN

.swi_entry
CMP     R11,#(end_swi_table-swi_table)/4
ADDCC   PC,PC,R11,LSL#2
B       unknown_swi
.swi_table
B       ArcEmKey_GetKey
.end_swi_table
:
.ArcEmKey_GetKey
STMFD   R13!,{R3-R9,R14}
LDR     R12,[R12]
LDR     R0,[R12,#r12_key_pointer]
CMP     R0,#8
MOVEQ   R0,#0
LDMEQFD R13!,{R3-R9,PC}

LDR     R1,[R12,#8]
LDR     R2,[R12,#12]

MOV     R4,#8
MOV     R5,#16
.ArcEmKey_GetKey_copy_keys
LDR     R6,[R12,R5]
STR     R6,[R12,R4]
ADD     R4,R4,#4
ADD     R5,R5,#4
CMP     R5,#252
BLT     ArcEmKey_GetKey_copy_keys

SUB     R0,R0,#8
STR     R0,[R12,#r12_key_pointer]

MOV     R0,#1
LDMFD   R13!,{R3-R9,PC}
:
.unknown_swi
ADR     R0,nosuchoperation
MSR     CPSR_f, #1<<28
MOV     PC, R14

.nosuchoperation
EQUD    &1E6
EQUS    "Unknown ArcEmKey operation"+CHR$0
ALIGN

]:NEXT
SYS "OS_File",10,"$.work.arcem.src.arcem.!ArcEm.ArcEmKey",&FFA,,code%,O%
END

DEF FNzero(n)
P%+=n:O%+=n
=0
